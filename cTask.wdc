#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : cTask
 major_version : 28
 minor_version : 0
 type : 4
 description : ""
 subtype : 0
class :
 identifier : 0x14f5dd5604ec1923
 internal_properties : CAAAAAgAAAB2/vstTMCJbS/hlxjFxirSqKvCUuv8YxgpWyl7S3iA
 code_elements :
  internal_properties : CAAAAAgAAABm4PVFe9JAHaoZ7pWrapr167xJBrSdz0axAnnnfYhOQaaZf6aFmw+87+hdnze0968KI3y7g0wMbri74fT3L3UE/39pGg==
  type_code : 10
  p_codes :
   -
     internal_properties : CAAAAAgAAAA7Evr0nw0WVZGKe5Uu5laDclTqSI0wCFFpGgxqxjFvMUCFrS2sS1mfYgFKs61E8dQbN6EHVF2aF0CBldZfrxd8SSURqQCX0d+tbip/adem7ccErRc5b/EA6SrHYX/n7tAVOR6sxo+LKv6Ys/hV2HrWrdzgsPqnNAwtfkZvS320tw==
     code : |1-
      // Class for emulating the "Tasks" folder of Outlook
      cTask is Class
      	inh√©rits of cEntity					// Inherits from the generic class for emulating Outlook
      	
      	m_sID is string				//Task Identifier
      	m_sCategory is string			// Category
      	m_sSubject is string				// Subject
      	m_sContent is string			// Content
      	m_sStartDate is string   		// Start date
      	m_sEndDate is string   		// End date (deadline)
      	m_nImportance is int			// Importance (Outlook #)
      	m_nReport is int				// Status (Outlook #)
      	m_rProgress is real			// Percentage of progress
      	m_bAlarm is boolean			// True if an alarm is requested
      	m_sAlarmDate is string		// Date of the alarm
      	m_sAlarmTime is string		// Time of the alarm
      	
      	CONSTANT
      		olImportanceLow		=0			// Low priority
      		olImportanceNormal	=1			// Standard priority
      		olImportanceHigh	=2			// High priority
      		olTaskNotStarted	=0			// Not started
      		olTaskInProgress	=1			// In progress
      		olTaskComplete		=2			// Done
      		olTaskWaiting		=3			// Pending
      		olTaskDeferred		=4			// Postponed
      	END
      END
     type : 131072
  procedures :
   -
     name : Constructor
     internal_properties : CAAAAAgAAADD5xXwfNmdH7pA7IYEKVxFiRRPuW9W5q4tzcPZrtzshLRpnc/0JYIpv/IBdhomLNWMBLRd54xkB4pCYFRX/WQXak2BBKEz/x/Rok0W1PiBertXAKTPI/HCQaFj90ohcWS1XQXNoGuoRoFxdnbtkEMKCN1fE9T7Lsd79c0+GrmcDR/21gPe4JO400QRwTfzAFxW0g==
     procedure_id : 1510356611572242739
     type_code : 27
     code : |1-
      PROCEDURE Constructor(MyItem= Null, clList = Null)
      
      // Call to the constructor of the parent class
      // to initialize the dialog with Outlook...
      Constructor cEntity(::CST_TASKS, MyItem)
      
      //This defines the list to which the element belongs
      //If this element is modified or deleted, we know which list to update..
      IF clList <> Null THEN :m_clList = clList
     type : 589824
   -
     name : Destructor
     procedure_id : 1510356611572308275
     type_code : 28
     type : 655360
   -
     name : AllocateProperties
     internal_properties : CAAAAAgAAAA82ZpM3iB8KZXLY3haIxhrsKUiMQts9yrEb7q0L8XhxqHS+NfPsZ/8SR6tm5vrK5zpVtRoGuA+B0BJblvD1vRLDUcAsdppqbva0ePjOKGJIG7KtE3Pn3ddZFBvIOLEF6yGkA32FMMKfQoKzFXDlYUiZUipdtKpvhbU3vXPpCSieUzBPPwwmifNbs9hFswwn+l4
     procedure_id : 1510356611572373811
     type_code : 12
     code : |1+
      // Summary: Assign the Outlook properties according to the values entered in WinDev
      // Syntax:
      // AllocateProperties ()
      //
      // Parameters:
      //	None
      // Return value:
      // 	None
      
      PROCEDURE VIRTUAL AllocateProperties()
      
      WHEN EXCEPTION IN
      	// Category
      	:m_pautItem>>Categories = :m_sCategory
      	// Subject
      	:m_pautItem>>Subject = :m_sSubject
      	// Content
      	:m_pautItem>>Body = :m_sContent
      	// Start date
      	IF DateValid(:m_sStartDate) THEN :m_pautItem>>StartDate = DateToInteger(:m_sStartDate)-DateToInteger("19000101")+2
      	// End date (deadline)
      	IF DateValid(:m_sEndDate) THEN :m_pautItem>>DueDate = DateToInteger(:m_sEndDate)-DateToInteger("19000101")+2
      	// Importance
      	:m_pautItem>>Importance = :m_nImportance-1
      	//Status
      	:m_pautItem>>Status = :m_nReport-1
      	// Progress
      	:m_pautItem>>PercentComplete = :m_rProgress
      	// Alarm
      	IF :m_bAlarm THEN
      		:m_pautItem>>ReminderSet=True
      		// Date & Time of alarm
      		:m_pautItem>>ReminderTime = DateToInteger(:m_sAlarmDate)-DateToInteger("19000101")+2  + ((Val(:m_sAlarmTime[[1 TO 2]])/24) + (Val(:m_sAlarmTime[[3 TO 4]])/24/60))
      	ELSE
      		:m_pautItem>>ReminderSet=False
      	END
      	
      DO
      	delete :m_pautItem
      	Error(ExceptionInfo(errSummary))
      	ReturnToCapture()
      END
     type : 458752
   -
     name : FillTask
     internal_properties : CAAAAAgAAADKEylCedC5XP23CJJYX9/Ta9Hel7yhrXRpDWm5BKuJHH165WwCJo/7GudwzEIcGV3PcKOvHdYQtQevz8JBst8s1y7Sn96e/vq9bEHH8zVDYgOJC/Kkmf9sfWOlvEBpXljLuJXWcUJfKD8BC2Ul0VUbebmZQfli0KdzF08kit9EN+MZHzQvymfiCsg/p2okjEbBxE504TRT1bpdZRhwTwCAtYWJ
     procedure_id : 1510356611572439347
     type_code : 12
     code : |1-
      // Summary: Assigning the members of the instance from the Outlook properties
      // Syntax:
      //FillTask (<clList>)
      //
      // Parameters:
      // 	clList: List to which the element belongs
      // Return value:
      // 	None
      
      PROCEDURE FillTask(clList)
      
      // Retrieve the properties of the task that was previously read
      sDateTime is datetime
      
      // Automatic identifier managed by Outlook
      :m_sID = :m_pautItem>>EntryID
      // Category
      :m_sCategory = :m_pautItem>>Categories
      // Subject
      :m_sSubject = :m_pautItem>>Subject
      // Content
      :m_sContent = :m_pautItem>>Body
      // Start date
      sDateTime=:m_pautItem>>StartDate
      :m_sStartDate = sDateTime..Date
      IF NOT DateValid(:m_sStartDate) OR :m_sStartDate>"29991231" THEN :m_sStartDate=""
      // End date (deadline)
      sDateTime=:m_pautItem>>DueDate
      :m_sEndDate = sDateTime..Date
      IF NOT DateValid(:m_sEndDate) OR :m_sEndDate>"29991231" THEN :m_sEndDate=""
      // Importance
      :m_nImportance = :m_pautItem>>Importance+1
      //Status
      :m_nReport = :m_pautItem>>Status+1
      // Progress
      :m_rProgress = :m_pautItem>>PercentComplete
      // Alarm
      :m_bAlarm = :m_pautItem>>ReminderSet
      // Alarm date
      sDateTime=:m_pautItem>>ReminderTime
      :m_sAlarmDate = sDateTime..Date
      IF NOT DateValid(:m_sAlarmDate) THEN :m_sAlarmDate=""
      // Alarm time
      :m_sAlarmTime=NumToString(sDateTime..Hour,"02d")+NumToString(sDateTime..Minute,"02d")
      
      
      :m_clList = clList
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : cTache
resources :
 string_res :
  identifier : 0x3c0216a90e2c6436
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
